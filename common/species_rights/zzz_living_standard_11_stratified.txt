living_standard_stratified = {
    token = living_standard_stratified
    name = living_standard_stratified
    in_breach_of = {
         {
            trigger = {
                is_gestalt = no
            }
            key = resolution_greatergood_greater_than_ourselves
        }
         {
            trigger = {
                is_gestalt = no
            }
            key = resolution_greatergood_balance_in_the_middle
        }
         {
            trigger = {
                is_gestalt = no
            }
            key = resolution_greatergood_universal_prosperity_mandate
        }
    }
    triggered_pop_modifier = {
        potential = {
            NAND = {
                exists = owner
                owner = {
                    has_deficit = consumer_goods
                }
            }
        }
        pop_cat_ruler_happiness = 0.15
        pop_cat_specialist_happiness = 0.05
    }
    pop_modifier = {
        pop_cat_worker_happiness = -0.10
        pop_cat_slave_happiness = -0.10
        pop_cat_worker_political_power = -0.5
        pop_cat_slave_political_power = -0.5
    }
    potential = {
        exists = from
        from = {
            is_gestalt = no
        }
        NOR = {
            has_trait = trait_hive_mind
            has_trait = trait_machine_unit
            is_shackled_robot_species_rights = yes
        }
    }
    allow = {
        hidden_trigger = {
            exists = from
        }
        custom_tooltip = {
            fail_text = empire_not_capitalist_authoritarian
            from = {
                OR = {
                    has_ethic = ethic_fanatic_capitalism
                    AND = {
                        OR = {
                            has_ethic = ethic_authoritarian
                            has_ethic = ethic_fanatic_authoritarian
                        }
                        NOR = {
                            has_ethic = ethic_socialism
                            has_ethic = ethic_fanatic_socialism
                        }
                    }
                }
            }
        }
        NOR = {
            custom_tooltip = {
                AND = {
                    has_citizenship_type = {
                        country = from
                        type = citizenship_slavery
                    }
                    NOT = {
                        has_slavery_type = {
                            country = from
                            type = slavery_military
                        }
                    }
                }
                text = is_slaves_not_battle_thralls
            }
            custom_tooltip = {
                AND = {
                    OR = {
                        has_citizenship_type = {
                            country = from
                            type = citizenship_purge_machine
                        }
                        has_citizenship_type = {
                            country = from
                            type = citizenship_purge
                        }
                    }
                    NOT = {
                        has_purge_type = {
                            country = from
                            type = purge_neutering
                        }
                    }
                }
                text = is_purged_not_neutered
            }
        }
        custom_tooltip = {
            fail_text = SPECIES_MUST_NOT_BE_ASSIMILATING
            NOT = {
                has_citizenship_type = {
                    type = citizenship_assimilation
                    country = from
                }
            }
        }
    }
    ai_will_do = {
        factor = 5
        modifier = {
            factor = 0.2
            from = {
                is_galactic_community_member = yes
            }
            OR = {
                is_active_resolution = "resolution_greatergood_greater_than_ourselves"
                is_active_resolution = "resolution_greatergood_balance_in_the_middle"
                is_active_resolution = "resolution_greatergood_universal_prosperity_mandate"
                AND = {
                    is_active_resolution = "resolution_divinity_a_defined_purpose"
                    FROM = {
                        has_country_flag = synthetic_empire
                    }
                }
            }
        }
    }
}
