decision_arcology_project = {
    owned_planets_only = yes
    icon = decision_arcology_project
    enactment_time = 3600
    resources = {
        category = decisions
        cost = {
            influence = 200
            minerals = 20000
        }
    }
    potential = {
        NOR = {
            is_planet_class = pc_pdshroud
            is_planet_class = pc_city
            is_planet_class = pc_relic
            is_planet_class = pc_living_station
            has_modifier = resort_colony
            has_modifier = penal_colony
            has_modifier = slave_colony
            has_modifier = pm_wenkwort_custodian
        }
        habitable_structure = no
        exists = owner
        owner = {
            OR = {
                has_ascension_perk = ap_arcology_project
                is_country_type = awakened_fallen_empire
            }
        }
    }
    allow = {
        custom_tooltip = {
            fail_text = decision_arcology_project_requirements
            free_district_slots < 1
            num_districts = {
                type = district_farming
                value < 1
            }
            num_districts = {
                type = district_mining
                value < 1
            }
            num_districts = {
                type = district_generator
                value < 1
            }
        }
        num_uncleared_blockers < 1
        NOR = {
            has_modifier = delicate_ecosystem
            has_modifier = eco_fragile_ecosystem
            has_deposit = d_underground_farm
            has_deposit = d_underground_mine
            has_deposit = d_underground_generator
            has_deposit = d_venerable_elder_site
            has_deposit = d_venerable_elder_migration_site
        }
    }
    effect = {
        custom_tooltip = decision_arcology_project_effects
        hidden_effect = {
            clear_deposits = yes
            change_pc = pc_city
            IF = {
                limit = {
                    owner = {
                        NOT = {
                            has_trait = trait_pc_aquatic_preference
                        }
                    }
                }
                set_planet_entity = {
                    entity = "city_planet_entity"
                    graphical_culture = owner
                }
            }
            IF = {
                limit = {
                    owner = {
                        has_trait = trait_pc_aquatic_preference
                    }
                }
                set_planet_entity = {
                    entity = "aquaticcity_planet_01_entity"
                    picture = "pc_aquaticcity"
                }
            }
            IF = {
                limit = {
                    owner = {
                        has_trait = trait_pc_ammonia_preference
                    }
                }
                IF = {
                    limit = {
                        NOT = {
                            has_modifier = pdammoniaatmosphere
                        }
                    }
                    add_modifier = {
                        modifier = pdammoniaatmosphere
                    }
                }
                remove_modifier = ammonia
                set_planet_entity = {
                    picture = pc_ammoniacity
                }
            }
            IF = {
                limit = {
                    owner = {
                        has_trait = trait_pc_sulfur_preference
                    }
                }
                IF = {
                    limit = {
                        NOT = {
                            has_modifier = pdsulfuratmosphere
                        }
                    }
                    add_modifier = {
                        modifier = pdsulfuratmosphere
                    }
                }
                remove_modifier = pdsulfur
                set_planet_entity = {
                    picture = pc_sulfurcity
                }
            }
            IF = {
                limit = {
                    owner = {
                        has_trait = trait_pc_methane_preference
                    }
                }
                IF = {
                    limit = {
                        NOT = {
                            has_modifier = pdmethaneatmosphere
                        }
                    }
                    add_modifier = {
                        modifier = pdmethaneatmosphere
                    }
                }
                remove_modifier = methane
                set_planet_entity = {
                    picture = pc_methanecity
                }
            }
            IF = {
                limit = {
                    owner = {
                        has_trait = trait_pc_ash_preference
                    }
                }
                IF = {
                    limit = {
                        NOT = {
                            has_modifier = pdashatmosphere
                        }
                    }
                    add_modifier = {
                        modifier = pdashatmosphere
                    }
                }
                remove_modifier = ash
                set_planet_entity = {
                    picture = pc_ashcity
                }
            }
            owner = {
                country_event = {
                    id = distar.201
                }
            }
            planet_event = {
                id = mega.200
            }
            if = {
                limit = {
                    OR = {
                        is_active_resolution = "resolution_ecology_environmental_control_board"
                        is_active_resolution = "resolution_ecology_paradise_initiative"
                    }
                }
                owner = {
                    set_timed_country_flag = {
                        flag = resolution_breached_terraformed
                        days = 3600
                    }
                }
            }
        }
    }
    ai_weight = {
        weight = 100
        modifier = {
            factor = 0
            OR = {
                is_planet_class = pc_gaia
                is_planet_class = pc_hive
                is_planet_class = pc_machine
            }
        }
        modifier = {
            factor = 0.001
            owner = {
                is_galactic_community_member = yes
            }
            OR = {
                is_active_resolution = "resolution_ecology_environmental_control_board"
                is_active_resolution = "resolution_ecology_paradise_initiative"
            }
        }
        modifier = {
            factor = 0
            owner = {
                is_galactic_community_member = yes
                OR = {
                    is_xenophile = yes
                    is_pacifist = yes
                    is_spiritualist = yes
                }
            }
            OR = {
                is_active_resolution = "resolution_ecology_environmental_control_board"
                is_active_resolution = "resolution_ecology_paradise_initiative"
            }
        }
    }
}
